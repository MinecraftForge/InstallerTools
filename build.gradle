plugins {
  id 'maven-publish'
  id 'java'
  id 'eclipse'
  id 'org.cadixdev.licenser' version '0.5.0'
  id 'org.ajoberstar.grgit' version '4.1.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group = 'net.minecraftforge'

version = (grgit.describe(longDescr: true, tags: true) ?: 'unknown-unknown-unknown').split('-').with { "${it[0]}.${it[1]}" }
println('Version: ' + version)
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
    maven { url 'https://maven.minecraftforge.net/' }
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/installertools/**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.installertools.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.installertools.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'net.md-5:SpecialSource:1.8.5'
    implementation 'de.siegmar:fastcsv:2.0.0'
    implementation 'net.minecraftforge:srgutils:0.4.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact shadowJar

            pom {
                name = 'Installer Tools'
                description = 'A collection of command line tools that are useful for the Forge installer, that are not worth being their own standalone projects.'
                url = 'https://github.com/MinecraftForge/InstallerTools'
                scm {
                    url = 'https://github.com/MinecraftForge/InstallerTools'
                    connection = 'scm:git:git://github.com/MinecraftForge/InstallerTools.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/InstallerTools.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/InstallerTools/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                url 'https://maven.minecraftforge.net/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
